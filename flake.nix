{
  description = "Five card draw is a simple poker style game. This library provides the basic functionality to play the game.";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
  inputs.flake-utils.url = "github:numtide/flake-utils";

  outputs =
    inputs:
    let
      overlay = final: prev: {
        haskell = prev.haskell // {
          packageOverrides =
            hfinal: hprev:
            prev.haskell.packageOverrides hfinal hprev
            // {
              fiveCardDraw = hfinal.callCabal2nix "fiveCardDraw" ./. { };
            };
        };
        fiveCardDraw = final.haskell.lib.compose.justStaticExecutables final.haskellPackages.fiveCardDraw;
      };
      perSystem =
        system:
        let
          pkgs = import inputs.nixpkgs {
            inherit system;
            overlays = [ overlay ];
          };
          hspkgs = pkgs.haskellPackages;
        in
        {
          devShell = hspkgs.shellFor {
            withHoogle = true;
            packages = p: [ p.fiveCardDraw ];
            buildInputs = [
              hspkgs.zlib
              hspkgs.cabal-install
              hspkgs.haskell-language-server
              hspkgs.hlint
              pkgs.bashInteractive
              hspkgs.ghcid
              hspkgs.hspec-discover
              hspkgs.fourmolu
              pkgs.hpack
              pkgs.jq
              pkgs.go-task
              pkgs.nixfmt-rfc-style
            ];
          };
          defaultPackage = pkgs.fiveCardDraw;
        };
    in
    { inherit overlay; } // inputs.flake-utils.lib.eachDefaultSystem perSystem;
}
